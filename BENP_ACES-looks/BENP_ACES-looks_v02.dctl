// ACES 1.2 Look Shaders
// utilises ACES library 1.2 from https://github.com/baldavenger/ACES_DCTL/blob/master/ACES_LIB.h

DEFINE_UI_PARAMS(cs_input, Input Colour Space, DCTLUI_COMBO_BOX, 0, { ACESCCTIN, ACESCCIN, LOGCAWGIN, LOG3G10RWGIN, SLOG3SG3CIN }, { ACEScct / AP1, ACEScc / AP1, ALEXA LogC / Alexa Wide Gamut, RED Log3g10 / Wide Gamut RGB, Sony SLog3 / SGamut3Cine})
DEFINE_UI_PARAMS(mid_grey, Tone Split, DCTLUI_SLIDER_FLOAT, 0.435, 0, 1, 0.001)
DEFINE_UI_PARAMS(tone_str, Tone Strength, DCTLUI_SLIDER_FLOAT, 1, 0, 2, 0.001)
DEFINE_UI_PARAMS(tone_select, Tone Profile, DCTLUI_COMBO_BOX, 1, { t001, t002 }, { T001, T002 })
DEFINE_UI_PARAMS(look_str, Look Strength, DCTLUI_SLIDER_FLOAT, 1, 0, 2, 0.001)
DEFINE_UI_PARAMS(look_select, Look Profile, DCTLUI_COMBO_BOX, 1, { l001, l002 }, { L001, L002 })
DEFINE_UI_PARAMS(skin_r, Skin Target R, DCTLUI_SLIDER_FLOAT, 113, 0, 255, 1)
DEFINE_UI_PARAMS(skin_g, Skin Target G, DCTLUI_SLIDER_FLOAT, 109, 0, 255, 1)
DEFINE_UI_PARAMS(skin_b, Skin Target B, DCTLUI_SLIDER_FLOAT, 103, 0, 255, 1)
DEFINE_UI_PARAMS(skin_str, Skin Bypass, DCTLUI_SLIDER_FLOAT, 0, 0, 1, 0.001)
DEFINE_UI_PARAMS(skin_show, Show Skin Matte, DCTLUI_CHECK_BOX, 0)
DEFINE_UI_PARAMS(cs_output, Output Colour Space, DCTLUI_COMBO_BOX, 0, { ACESCCTOUT, ACESCCOUT, LOGCAWGOUT, LOG3G10RWGOUT, SLOG3SG3COUT, RRTODT_REC709 }, { ACEScct / AP1, ACEScc / AP1, ALEXA LogC / Alexa Wide Gamut, RED Log3g10 / Wide Gamut RGB, Sony SLog3 / SGamut3Cine, Gamma 2.4 100nits / Rec709 })

#include "ACES_LIB.h"
#include "BENP_LIB.h"

__DEVICE__ float3 transform(int p_Width, int p_Height, int p_X, int p_Y, float p_R, float p_G, float p_B)
{

float3 ipt = make_float3(p_R, p_G, p_B);


float3 pre_rgb;
  if (cs_input == ACESCCTIN) pre_rgb = ACEScct_to_ACES(ipt);
  if (cs_input == ACESCCIN) pre_rgb = ACEScc_to_ACES(ipt);
  if (cs_input == LOGCAWGIN) pre_rgb = LogC_EI800_AWG_to_ACES(ipt);
  if (cs_input == LOG3G10RWGIN) pre_rgb = Log3G10_RWG_to_ACES(ipt);
  if (cs_input == SLOG3SG3CIN) pre_rgb = SLog3_SG3C_to_ACES(ipt);
  pre_rgb = ACES_to_ACEScct(pre_rgb);


float3 tone1_rgb;
  if (tone_select == t001) tone1_rgb = ((tone_str * 0.2f) * tone_001(pre_rgb, mid_grey)) + (pre_rgb * ( 1 - (tone_str * 0.2f)));
  if (tone_select == t002) tone1_rgb = ((tone_str * 0.2f) * tone_002(pre_rgb, mid_grey)) + (pre_rgb * ( 1 - (tone_str * 0.2f)));


float3 look_rgb;
  if (look_select == l001) look_rgb = (look_str * look_001(tone1_rgb)) + (tone1_rgb * ( 1 - look_str));
  if (look_select == l002) look_rgb = (look_str * look_002(tone1_rgb)) + (tone1_rgb * ( 1 - look_str));


float3 tone2_rgb;
  if (tone_select == t001) tone2_rgb = ((tone_str * 0.8f) * tone_001(look_rgb, mid_grey)) + (look_rgb * ( 1 - (tone_str * 0.8f)));
  if (tone_select == t002) tone2_rgb = ((tone_str * 0.8f) * tone_002(look_rgb, mid_grey)) + (look_rgb * ( 1 - (tone_str * 0.8f)));


float3 skin_rgb;
float3 skin_target = make_float3((skin_r / 255), (skin_g / 255), (skin_b / 255));
float skin_alpha = skin_bypass(pre_rgb, skin_target, skin_str) * skin_str;
  skin_rgb = ((skin_alpha * pre_rgb) + (tone2_rgb * ( 1 - skin_alpha)));


float3 post_rgb = ACEScct_to_ACES(skin_rgb);


float3 opt;
  if (cs_output == ACESCCTOUT) opt = ACES_to_ACEScct(post_rgb);
  if (cs_output == ACESCCOUT) opt = ACES_to_ACEScc(post_rgb);
  if (cs_output == LOGCAWGOUT) opt = ACES_to_LogC_EI800_AWG(post_rgb);
  if (cs_output == LOG3G10RWGOUT) opt = ACES_to_Log3G10_RWG(post_rgb);
  if (cs_output == SLOG3SG3COUT) opt = ACES_to_SLog3_SG3C(post_rgb);
  if (cs_output == RRTODT_REC709) opt = RRTODT_Rec709_100nits_10nits_BT1886(post_rgb);


if (skin_show == 1) {opt = skin_alpha;}
return opt;
}
